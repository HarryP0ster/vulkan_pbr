#version 460
#include "noise.glsl"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout (constant_id = 0) const uint frequency_worley = 4;
layout (constant_id = 1) const uint frequency_perlin = 4;
layout (constant_id = 2) const uint worley_octaves = 4;
layout (constant_id = 3) const uint perlin_octaves = 4;
layout (constant_id = 4) const uint seed = 0;

layout (binding = 0, rgba8) uniform writeonly image3D outImage;

void main()
{
    NOISE_SEED = seed;
    
    const vec3 cell_loc = vec3(gl_GlobalInvocationID.xyz) / vec3(gl_NumWorkGroups.xyz) + uintBitsToFloat(1000000);
    float worley01 = worley(cell_loc, frequency_worley);
    float worley02 = worley(cell_loc, frequency_worley * 2.0);
    float worley03 = worley(cell_loc, frequency_worley * 4.0);
    float worley04 = worley(cell_loc, frequency_worley * 6.0);
    float worley05 = worley(cell_loc, frequency_worley * 8.0);
    float worley06 = worley(cell_loc, frequency_worley * 16.0);
    
    float perlin = fbm_perlin(cell_loc, frequency_perlin, perlin_octaves);

    float fbm01 = worley01 * 0.625 + worley02 * 0.25 + worley03 * 0.125;
    float fbm02 = worley02 * 0.625 + worley03 * 0.25 + worley05 * 0.125;
    float fbm03 = worley05 * 0.75 + worley06 * 0.25;

    float perlin_worley = remap(perlin, 0.0, 1.0, worley01 * 0.625 + worley04 * 0.25 + worley06 * 0.125, 1.0);

    imageStore(outImage, ivec3(gl_GlobalInvocationID.xyz), vec4(perlin_worley, fbm01, fbm02, fbm03));
}